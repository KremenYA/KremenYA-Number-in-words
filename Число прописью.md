
### Функция ЧислоПрописью 
Синтаксис, аргументы, примеры использования представлены в файле README.md
### Код
```
ЧислоПрописью =LAMBDA(Число;[Валюта];
  LET(
    ЧислоСтрока; ТЕКСТ(Число; "000000000000000,00");
    МассивКлассов; MAKEARRAY(6; 1; LAMBDA(I;j; --ПОДСТАВИТЬ(ПСТР(ЧислоСтрока; (I-1) * 3 + 1; 3); ","; "")));

    Классы; МеняемВалюту(Валюта);

    ЧислоПрописью;
    КлассПрописью(ИНДЕКС(МассивКлассов; 1); ИНДЕКС(Классы; 1; ПОСЛЕД(; 4)); ЛОЖЬ)
    & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 2); ИНДЕКС(Классы; 2; ПОСЛЕД(; 4)); ЛОЖЬ)
    & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 3); ИНДЕКС(Классы; 3; ПОСЛЕД(; 4)); ЛОЖЬ)
    & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 4); ИНДЕКС(Классы; 4; ПОСЛЕД(; 4)); ЛОЖЬ)
    & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 5); ИНДЕКС(Классы; 5; ПОСЛЕД(; 4)); ЦЕЛОЕ(Число)>0)
    & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 6); ИНДЕКС(Классы; 6; ПОСЛЕД(; 4)); ЛОЖЬ);

    ЕСЛИ(Число > 0; СЖПРОБЕЛЫ(ЧислоПрописью); "ноль " & ИНДЕКС(Классы; 5; 3) )
  )
)
```
 
### Функция МеняемВалюту
### Код
```
МеняемВалюту=LAMBDA(КодВалюты;
  LET(
    Классы; {"триллион";"триллиона";"триллионов";"м":"миллиард";"миллиарда";"миллиардов";"м":"миллион";"миллиона";"миллионов";"м":"тысяча";"тысячи";"тысяч";"ж":"рубль";"рубля";"рублей";"м":"копейка";"копейки";"копеек";"ж"};
    КлассПриВозникновенииОшибкиВМассиве;{"целая";"целых";"целых";"ж":"сотая";"сотых";"сотых";"ж"};
    КлассНеизвестнаяСтрокаКодВалюты; {"рубль";"рубля";"рублей";"м":"копейка";"копейки";"копеек";"ж"};
    ИзмененныеКлассы;
    ЕСЛИМН(
      ТИП(КодВалюты) = 64; ЕСЛИ(И(ЧСТРОК(КодВалюты) = 2; ЧИСЛСТОЛБ(КодВалюты) = 4); КодВалюты; КлассПриВозникновенииОшибкиВМассиве);

      ЕТЕКСТ(КодВалюты);
      ЕСЛИМН(
        ПРОПИСН(КодВалюты) = "BYN";
          {"белорусский рубль";"белорусских рубля";"белорусских рублей";"м":"копейка";"копейки";"копеек";"ж"};
        ПРОПИСН(КодВалюты) = "USD";
          {"доллар США";"доллара США";"долларов США";"м":"цент";"цента";"центов";"м"};
        ПРОПИСН(КодВалюты) = "EUR";
          {"евро";"евро";"евро";"м":"евроцент";"евроцента";"евроцентов";"м"};
        ПРОПИСН(КодВалюты) = "RUB";
          {"российский рубль";"российских рубля";"российских рублей";"м":"копейка";"копейки";"копеек";"ж"};
        ИСТИНА; КлассНеизвестнаяСтрокаКодВалюты
        )

    );
    ВозвращаемКлассыСИзменениями;
    LET(
      НомерСтроки; ПОСЛЕД(6);
      НомерСтолбца; ПОСЛЕД(1;4);
      ЕСЛИ(
        НомерСтроки >= 5;
        ИНДЕКС(ИзмененныеКлассы; НомерСтроки - 4; НомерСтолбца);
        ИНДЕКС(Классы; НомерСтроки; НомерСтолбца)
      )
    );
    ЕСЛИОШИБКА(ВозвращаемКлассыСИзменениями; Классы)
  )
)
```

### КлассПрописью
### Код
```
=LAMBDA(Класс;МассивНаписанияКласса;ПоказатьПриНуле;
  LET(
    Сотни; ЦЕЛОЕ(Класс / 100);
    Десятки; ЦЕЛОЕ(ОСТАТ(Класс; 100) / 10);
    Единицы; ОСТАТ(Класс; 10);
    ЕстьВторойДесяток; И(Десятки = 1; Единицы <> 0);
    ДесяткиДляЗаписи; ЕСЛИ(ЕстьВторойДесяток; 0; Десятки);
    ВторойДесятокДляЗаписи; ЕСЛИ(ЕстьВторойДесяток; Единицы; 0);
    ЕдиницыДляЗаписи; ЕСЛИ(ЕстьВторойДесяток; 0; Единицы);

    МассивСотен; {"";"сто";"двести";"триста";"четыреста";"пятьсот";"шестьсот";"семьсот";"восемьсот";"девятьсот"};
    МассивДесятков; {"";"десять";"двадцать";"тридцать";"сорок";"пятьдесят";"шестьдесят";"семьдесят";"восемьдесят";"девяносто"};
    МассивЕдиницМуж; {"";"один";"два";"три";"четыре";"пять";"шесть";"семь";"восемь";"девять"};
    МассивЕдиницЖен; {"";"одна";"две";"три";"четыре";"пять";"шесть";"семь";"восемь";"девять"};
    МассивЕдиницСред; {"";"одно";"два";"три";"четыре";"пять";"шесть";"семь";"восемь";"девять"};
    МассивВторогоДесятка; {"";"одиннадцать";"двенадцать";"тринадцать";"четырнадцать";"пятнадцать";"шестнадцать";"семнадцать";"восемнадцать";"девятнадцать"};

    МассивЕдиниц;
    ЕСЛИМН(
      ИНДЕКС(МассивНаписанияКласса; 1; 4) = "м"; МассивЕдиницМуж;
      ИНДЕКС(МассивНаписанияКласса; 1; 4) = "ж"; МассивЕдиницЖен;
      ИНДЕКС(МассивНаписанияКласса; 1; 4) = "с"; МассивЕдиницСред;
      ИСТИНА; МассивЕдиницМуж
    );

    КлассПрописью;
    ИНДЕКС(МассивСотен; 1; Сотни + 1)
    & " " & ИНДЕКС(МассивДесятков; 1; ДесяткиДляЗаписи + 1)
    & " " & ИНДЕКС(МассивЕдиниц; 1; ЕдиницыДляЗаписи + 1)
    & " " & ИНДЕКС(МассивВторогоДесятка; 1; ВторойДесятокДляЗаписи + 1);

    НазваниеКласса;
    ЕСЛИМН(
      ЕдиницыДляЗаписи = 1; ИНДЕКС(МассивНаписанияКласса; 1);
      И(ЕдиницыДляЗаписи >= 2; ЕдиницыДляЗаписи <= 4); ИНДЕКС(МассивНаписанияКласса; 2);
      ИСТИНА; ИНДЕКС(МассивНаписанияКласса; 3)
    );

    ЕСЛИ(
      ИЛИ(Класс <> 0; И(Класс = 0; ПоказатьПриНуле));
      КлассПрописью & " " & НазваниеКласса;
      КлассПрописью
    )
  )
)
```
