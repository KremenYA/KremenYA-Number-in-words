### Функция ЧислоПрописью (одна функция) 
Синтаксис, аргументы, примеры использования представлены в файле README.md
Данная LAMBDA-функция не может быть определена в Диспетчере имён Excel  из-за ограничения на длину формулы в 2084 символа. Однако она может быть введена и вызвана в ячейке Excel. Надеюсь, что будет увеличен размер формулы, которую можно определить в Диспетчере имён, и после этого данный вариант станет допустим к использованию в Диспетчере имён.


### Код
```
=LAMBDA(Число;[Валюта];
    LET(
        ЧислоСтрока; ТЕКСТ(Число; "000000000000000,00");
        МассивКлассов; MAKEARRAY(6; 1; LAMBDA(I;j; --ПОДСТАВИТЬ(ПСТР(ЧислоСтрока; (I-1) * 3 + 1; 3); ","; "")));

        КлассыПоУмолчанию; 
            {"триллион";"триллиона";"триллионов";"м":
             "миллиард";"миллиарда";"миллиардов";"м":
             "миллион";"миллиона";"миллионов";"м":
             "тысяча";"тысячи";"тысяч";"ж":
             "рубль";"рубля";"рублей";"м":
             "копейка";"копейки";"копеек";"ж"};

        МеняемВалюту;
        LAMBDA(КлассыПоУмолчанию;Валюта;
            LET(
                КлассПриВозникновенииОшибкиВМассиве;
                    {"целая";"целых";"целых";"ж":
                     "сотая";"сотых";"сотых";"ж"};
                КлассНеизвестнаяСтрокаВалюта; 
                    {"доллар";"доллара";"долларов";"м":
                     "цент";"цента";"центов";"м"};
                ИзмененныеКлассы;
                ЕСЛИМН(
                    ТИП(Валюта) = 64; ЕСЛИ(И(ЧСТРОК(Валюта) = 2; ЧИСЛСТОЛБ(Валюта) = 4); Валюта; КлассПриВозникновенииОшибкиВМассиве);

                    ЕТЕКСТ(Валюта);
                    ЕСЛИМН(
                        ПРОПИСН(Валюта) = "BYN";
                            {"белорусский рубль";"белорусских рубля";"белорусских рублей";"м":
                             "копейка";"копейки";"копеек";"ж"};
                        ПРОПИСН(Валюта) = "USD";
                            {"доллар США";"доллара США";"долларов США";"м":
                             "цент";"цента";"центов";"м"};
                        ПРОПИСН(Валюта) = "EUR";
                            {"евро";"евро";"евро";"м":
                             "евроцент";"евроцента";"евроцентов";"м"};
                        ПРОПИСН(Валюта) = "RUB";
                            {"российский рубль";"российских рубля";"российских рублей";"м":
                             "копейка";"копейки";"копеек";"ж"};
                        ИСТИНА; КлассНеизвестнаяСтрокаВалюта
                        )

                 );
                ВозвращаемКлассыСИзменениями;
                LET(
                    НомерСтроки; ПОСЛЕД(6);
                    НомерСтолбца; ПОСЛЕД(1;4);
                    ЕСЛИ(
                        НомерСтроки >= 5;
                        ИНДЕКС(ИзмененныеКлассы; НомерСтроки - 4; НомерСтолбца);
                        ИНДЕКС(КлассыПоУмолчанию; НомерСтроки; НомерСтолбца)
                    )
                );
                ЕСЛИОШИБКА(ВозвращаемКлассыСИзменениями; КлассыПоУмолчанию)
            )
        );

        Классы; ЕСЛИ(
                    ПРОПУЩЕНО(Валюта);
                    КлассыПоУмолчанию;
                    МеняемВалюту(КлассыПоУмолчанию;Валюта)
                );

        КлассПрописью;
        LAMBDA(Класс;МассивНаписанияКласса;ПоказатьПриНуле;
            LET(
                Сотни; ЦЕЛОЕ(Класс / 100);
                Десятки; ЦЕЛОЕ(ОСТАТ(Класс; 100) / 10);
                Единицы; ОСТАТ(Класс; 10);
                ЕстьВторойДесяток; И(Десятки = 1; Единицы <> 0);
                ДесяткиДляЗаписи; ЕСЛИ(ЕстьВторойДесяток; 0; Десятки);
                ВторойДесятокДляЗаписи; ЕСЛИ(ЕстьВторойДесяток; Единицы; 0);
                ЕдиницыДляЗаписи; ЕСЛИ(ЕстьВторойДесяток; 0; Единицы);

                МассивСотен; {"";"сто";"двести";"триста";"четыреста";"пятьсот";"шестьсот";"семьсот";"восемьсот";"девятьсот"};
                МассивДесятков; {"";"десять";"двадцать";"тридцать";"сорок";"пятьдесят";"шестьдесят";"семьдесят";"восемьдесят";"девяносто"};
                МассивВторогоДесятка; {"";"одиннадцать";"двенадцать";"тринадцать";"четырнадцать";"пятнадцать";"шестнадцать";"семнадцать";"восемнадцать";"девятнадцать"};

                Род; ИНДЕКС(МассивНаписанияКласса; 1; 4);
                МассивЕдиниц;
                ЕСЛИМН(
                    Род = "ж"; {"";"одна";"две";"три";"четыре";"пять";"шесть";"семь";"восемь";"девять"};
                    ИЛИ(Род = "с";Род = "c"); {"";"одно";"два";"три";"четыре";"пять";"шесть";"семь";"восемь";"девять"};
                    ИСТИНА; {"";"один";"два";"три";"четыре";"пять";"шесть";"семь";"восемь";"девять"}
                );

                КлассПрописью;
                ИНДЕКС(МассивСотен; 1; Сотни + 1)
                & " " & ИНДЕКС(МассивДесятков; 1; ДесяткиДляЗаписи + 1)
                & " " & ИНДЕКС(МассивЕдиниц; 1; ЕдиницыДляЗаписи + 1)
                & " " & ИНДЕКС(МассивВторогоДесятка; 1; ВторойДесятокДляЗаписи + 1);

                НазваниеКласса;
                ЕСЛИМН(
                    ЕдиницыДляЗаписи = 1; ИНДЕКС(МассивНаписанияКласса; 1);
                    И(ЕдиницыДляЗаписи >= 2; ЕдиницыДляЗаписи <= 4); ИНДЕКС(МассивНаписанияКласса; 2);
                    ИСТИНА; ИНДЕКС(МассивНаписанияКласса; 3)
                );

                ЕСЛИ(
                    ИЛИ(Класс <> 0; И(Класс = 0; ПоказатьПриНуле));
                    КлассПрописью & " " & НазваниеКласса;
                    КлассПрописью
                )
            )
        );

        ЧислоПрописью;
        КлассПрописью(ИНДЕКС(МассивКлассов; 1); ИНДЕКС(Классы; 1); ЛОЖЬ)
        & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 2); ИНДЕКС(Классы; 2); ЛОЖЬ)
        & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 3); ИНДЕКС(Классы; 3); ЛОЖЬ)
        & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 4); ИНДЕКС(Классы; 4); ЛОЖЬ)
        & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 5); ИНДЕКС(Классы; 5); ЦЕЛОЕ(Число)>0)
        & " " & КлассПрописью(ИНДЕКС(МассивКлассов; 6); ИНДЕКС(Классы; 6); ЛОЖЬ);

        ЕСЛИ(Число > 0; СЖПРОБЕЛЫ(ЧислоПрописью); "ноль " & ИНДЕКС(Классы; 5; 3) )
    )
)
```
